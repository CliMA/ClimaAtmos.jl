import ClimaCore.MatrixFields: @name
import ClimaCore.RecursiveApply: ⊞, ⊠, rzero, rpromote_type

"""
    specific(ρχ, ρ)
    specific(ρaχ, ρa, ρχ, ρ, turbconv_model)

Calculates the specific quantity `χ` (per unit mass) from a density-weighted
quantity. This function uses multiple dispatch to select the appropriate
calculation method based on the number of arguments.

**Grid-Scale Method (2 arguments)**

    specific(ρχ, ρ)

Performs a direct division of the density-weighted quantity `ρχ` by the density
`ρ`. This method is used for grid-mean quantities where the density `ρ` is
well-defined and non-zero.

**SGS Regularized Method (5 arguments)**

    specific(ρaχ, ρa, ρχ, ρ, turbconv_model)

Calculates the specific quantity `χ` for a subgrid-scale (SGS) component by
dividing the density-area-weighted quantity `ρaχ` by the density-area product
`ρa`.

This method includes regularization to handle cases where the SGS area fraction
(and thus `ρa`) is zero or vanishingly small. It performs a linear
interpolation between the SGS specific quantity (`ρaχ / ρa`) and the grid-mean
specific quantity (`ρχ / ρ`). The interpolation weight is computed by
`sgs_weight_function` to ensure a smooth and numerically stable transition,
preventing division by zero. Using this regularized version instead of directly
computing `ρaχ / ρa` breaks the assumption of domain decomposition (sum of SGS
domains equals GS) when the approximated area fraction `a` is small.

Arguments:

  - `ρχ`: The grid-mean density-weighted quantity (e.g., `ρe_tot`, `ρq_tot`).
  - `ρ`: The grid-mean density.
  - `ρaχ`: The density-area-weighted SGS quantity (e.g., `sgs.ρa * sgs.h_tot`).
  - `ρa`: The density-area product of the SGS component.
  - `ρχ_fallback`: The grid-mean density-weighted quantity used for the fallback value.
  - `ρ_fallback`: The grid-mean density used for the fallback value.
  - `turbconv_model`: The turbulence convection model, containing parameters for regularization (e.g., `a_half`).
"""
specific(ρχ, ρ) = ρχ / ρ

function specific(ρaχ, ρa, ρχ, ρ, turbconv_model)
    # TODO: Replace turbconv_model struct by parameters, and include a_half in
    # parameters, not in config
    weight = sgs_weight_function(ρa / ρ, turbconv_model.a_half)
    # If ρa is exactly zero, the weight is zero, making the first term NaN (0 * … / 0).
    # For negligible ρa the weight is also zero, and autodiff can still produce NaNs.
    # The ifelse handles both cases explicitly by returning the grid-mean value when ρa < eps.
    return ρa < eps(typeof(ρ)) ? ρχ / ρ : weight * ρaχ / ρa + (1 - weight) * ρχ / ρ
end

"""
    env_relaxation_feedback(ρaʲ, ρa⁰, ρ, turbconv_model)

Magnitude of `-∂χ⁰/∂χʲ` for an environment value diagnosed by [`specific`](@ref)
from the domain decomposition,

    χ⁰ = w · (ρχ - Σⱼ ρaʲ χʲ) / ρa⁰ + (1 - w) · ρχ / ρ,

differentiated at fixed grid-mean `ρχ` and fixed `ρaʲ`:

    ∂χ⁰/∂χʲ = -w · ρaʲ / ρa⁰,

with `w = sgs_weight_function(ρa⁰/ρ, a_half)` and the same `ρa⁰ < eps` fallback
branch as `specific` (where the derivative is zero). This is the exact
derivative of `specific`'s regularized quotient, so the gating keeps the
result finite as `ρa⁰ → 0`. Used by the entrainment-relaxation diagonals of
the implicit Jacobian in `manual_sparse_jacobian.jl`.
"""
@inline env_relaxation_feedback(ρaʲ, ρa⁰, ρ, turbconv_model) =
    ρa⁰ < eps(typeof(ρ)) ? zero(ρ) :
    sgs_weight_function(ρa⁰ / ρ, turbconv_model.a_half) * ρaʲ / ρa⁰

# Internal method that checks if its input is @name(ρχ) for some variable χ.
@generated is_ρ_weighted_name(
    ::MatrixFields.FieldName{name_chain},
) where {name_chain} =
    length(name_chain) == 1 && startswith(string(name_chain[1]), "ρ")

# Internal method that converts @name(ρχ) to @name(χ) for some variable χ.
@generated function specific_tracer_name(
    ::MatrixFields.FieldName{ρχ_name_chain},
) where {ρχ_name_chain}
    χ_symbol = Symbol(string(ρχ_name_chain[1])[(ncodeunits("ρ") + 1):end])
    return :(@name($χ_symbol))
end

"""
    gs_tracer_names(Y)

`Tuple` of `@name`s for the grid-scale tracers in the center field `Y.c`
(excluding `ρ`, `ρe_tot`, `ρtke`, velocities, and SGS fields).
"""
gs_tracer_names(Y) =
    unrolled_filter(MatrixFields.top_level_names(Y.c)) do name
        is_ρ_weighted_name(name) && !(name in (@name(ρ), @name(ρe_tot), @name(ρtke)))
    end

"""
    specific_gs_tracer_names(Y)

`Tuple` of the specific tracer names `@name(χ)` that correspond to the
density-weighted tracer names `@name(ρχ)` in `gs_tracer_names(Y)`.
"""
specific_gs_tracer_names(Y) =
    unrolled_map(specific_tracer_name, gs_tracer_names(Y))

"""
    ᶜempty(Y)

Lazy center `Field` of empty `NamedTuple`s.
"""
ᶜempty(Y) = lazy.(Returns((;)).(Y.c))

"""
    ᶜgs_tracers(Y)

Lazy center `Field` of `NamedTuple`s that contain the values of all grid-scale
tracers given by `gs_tracer_names(Y)`.
"""
function ᶜgs_tracers(Y)
    isempty(gs_tracer_names(Y)) && return ᶜempty(Y)
    ρχ_symbols = unrolled_map(MatrixFields.extract_first, gs_tracer_names(Y))
    ρχ_fields = unrolled_map(gs_tracer_names(Y)) do ρχ_name
        MatrixFields.get_field(Y.c, ρχ_name)
    end
    return @. lazy(NamedTuple{ρχ_symbols}(tuple(ρχ_fields...)))
end

"""
    ᶜspecific_gs_tracers(Y)

Lazy center `Field` of `NamedTuple`s that contain the values of all specific
grid-scale tracers given by `specific_gs_tracer_names(Y)`.
"""
function ᶜspecific_gs_tracers(Y)
    isempty(gs_tracer_names(Y)) && return ᶜempty(Y)
    χ_symbols =
        unrolled_map(MatrixFields.extract_first, specific_gs_tracer_names(Y))
    χ_fields = unrolled_map(gs_tracer_names(Y)) do ρχ_name
        ρχ_field = MatrixFields.get_field(Y.c, ρχ_name)
        @. lazy(specific(ρχ_field, Y.c.ρ))
    end
    return @. lazy(NamedTuple{χ_symbols}(tuple(χ_fields...)))
end

"""
    foreach_gs_tracer(f, Y_or_similar_values...)

Applies a function `f` to each grid-scale tracer in the state `Y` or any similar
value like the tendency `Yₜ`. This is used to implement performant loops over
all tracers given by `gs_tracer_names(Y)`.

Although the first input value needs to be similar to `Y`, the remaining values
can also be center `Field`s similar to `Y.c`, and they can use specific tracers
given by `specific_gs_tracer_names(Y)` instead of density-weighted tracers.

Arguments:

  - `f`: The function applied to each grid-scale tracer, which must have the
    signature `f(ρχ_or_χ_fields..., ρχ_name)`, where `ρχ_or_χ_fields` are
    grid-scale tracer subfields (either density-weighted or specific) and
    `ρχ_name` is the `MatrixFields.FieldName` of the tracer.
  - `Y_or_similar_values`: The state `Y` or similar values like the tendency `Yₜ`.

# Examples

```julia
foreach_gs_tracer(Yₜ, Y) do ᶜρχₜ, ᶜρχ, ρχ_name
    ᶜρχₜ .+= tendency_of_ρχ(ᶜρχ)
    if ρχ_name == @name(ρq_tot)
        ᶜρχₜ .+= additional_tendency_of_ρq_tot(ᶜρχ)
    end
end
```

```julia
foreach_gs_tracer(Yₜ, Base.materialize(ᶜspecific_gs_tracers(Y))) do ᶜρχₜ, ᶜχ, ρχ_name
    ᶜρχₜ .+= Y.c.ρ .* tendency_of_χ(ᶜχ)
    if ρχ_name == @name(ρq_tot)
        ᶜρχₜ .+= Y.c.ρ .* additional_tendency_of_q_tot(ᶜχ)
    end
end
```
"""
foreach_gs_tracer(f::F, Y_or_similar_values...) where {F} =
    unrolled_foreach(gs_tracer_names(Y_or_similar_values[1])) do ρχ_name
        ρχ_or_χ_fields = unrolled_map(Y_or_similar_values) do value
            field = value isa Fields.Field ? value : value.c
            ρχ_or_χ_name =
                MatrixFields.has_field(field, ρχ_name) ? ρχ_name :
                specific_tracer_name(ρχ_name)
            MatrixFields.get_field(field, ρχ_or_χ_name)
        end
        f(ρχ_or_χ_fields..., ρχ_name)
    end


"""
    sgs_tracer_names(Y)

Return a `Tuple` of `FieldName`s for all SGS (sub-grid scale) tracers
present in the first updraft of `Y`. Returns `()` when prognostic EDMF
is not active (i.e. when `Y.c` has no `sgsʲs` field).

"Tracer" here means any scalar in `Y.c.sgsʲs.:(1)` that is **not** one
of the core EDMF variables `ρa`, `mse`, or `q_tot` (which receive
physics-specific treatment).
"""
_is_sgs_tracer_name(::MatrixFields.FieldName{name_chain}) where {name_chain} =
    !(last(name_chain) in (:ρa, :mse, :q_tot))

sgs_tracer_names(Y) =
    _sgs_tracer_names(Val(hasproperty(Y.c, :sgsʲs)), Y)
_sgs_tracer_names(::Val{false}, Y) = ()
_sgs_tracer_names(::Val{true}, Y) =
    unrolled_filter(
        _is_sgs_tracer_name,
        MatrixFields.top_level_names(Y.c.sgsʲs.:(1)),
    )


"""
    sgs_weight_function(a, a_half)

Computes a smooth, monotonic weight function `w(a)` that ranges from 0 to 1.

This function is used as the interpolation weight in the regularized `specific`
function. It ensures a numerically stable and smooth transition between a subgrid-scale
(SGS) quantity and its grid-mean counterpart, especially when the SGS area fraction `a`
is small.

**Key Properties:**

  - `w(a) = 0` for `a ≤ 0`.
  - `w(a) = 1` for `a ≥ 1`.
  - `w(a_half) = 0.5`.
  - The function is continuously differentiable, with derivatives equal to zero at
    `a = 0` and `a = 1`, which ensures smooth blending.
  - The functions grows very rapidly near `a = a_half`, and grows very slowly at all other
    values of `a`.
  - For small `a_half`, the weight rapidly approaches 1 for values of `a` that are
    a few times larger than `a_half`.

**Construction Method:**
The function is piecewise. For `a` between 0 and 1, it is a custom sigmoid curve
constructed in two main steps to satisfy the key properties:

 1. **Bounded Sigmoid Creation**: A base sigmoid is created that maps the interval
    `(0, 1)` to `(0, 1)` with zero derivatives at the endpoints. This is achieved
    by composing a standard `tanh` function with the inverse of a slower-growing
    `tanh` function.
 2. **Midpoint Control**: To ensure the function passes through the control point
    `(a_half, 0.5)`, the input `a` is first transformed by a specially designed
    power function (`1 - (1 - a)^k`) before being passed to the bounded sigmoid.
    This transformation maps `a_half` to `0.5` while preserving differentiability
    at the boundaries.

Arguments:

  - `a`: The input SGS area fraction (often approximated as `ρa / ρ`).
  - `a_half`: The value of `a` at which the weight function should be 0.5, controlling
    the transition point of the sigmoid curve.

Returns:

  - The computed weight, a value between 0 and 1.
"""
function sgs_weight_function(a, a_half)
    if a < 0
        zero(a)
    elseif a > min(1, 42 * a_half) # autodiff generates NaNs when a is large
        one(a)
    else
        (1 + tanh(2 * atanh(1 - 2 * (1 - a)^(-1 / log2(1 - a_half))))) / 2
    end
end

"""
    draft_sum(f, sgsʲs)

Computes the sum of a function `f` applied to each draft subdomain
state `sgsʲ` in the iterator `sgsʲs`.

Arguments:

  - `f`: A function to apply to each element of `sgsʲs`.
  - `sgsʲs`: An iterator over the draft subdomain states.
"""
draft_sum(f, sgsʲs) = unrolled_sum(f, sgsʲs)

"""
    ᶜenv_value(grid_scale_value, f_draft, gs, turbconv_model)

Computes the value of a quantity `ρaχ` in the environment subdomain by subtracting
the sum of its values in all draft subdomains from the grid-scale value. Available
for general variables in PrognosticEDMFX.

This is based on the domain decomposition principle for density-area weighted
quantities: `GridMean(ρχ) = Env(ρaχ) + Sum(Drafts(ρaχ))`.

For PrognosticEDMFX: Uses gs.sgsʲs to access draft subdomain states

Arguments:

  - `grid_scale_value`: The `ρa`-weighted grid-scale value of the quantity.
  - `f_draft`: A function that extracts the corresponding value from a draft subdomain state.
  - `gs`: The grid-scale iteration object, which contains the draft subdomain states `gs.sgsʲs` (for PrognosticEDMFX) from the state `Y.c`.
  - `turbconv_model`: The turbulence convection model, used to determine how to access draft data.
"""
function ᶜenv_value(grid_scale_value, f_draft, gs)
    return @. lazy(grid_scale_value - draft_sum(f_draft, gs))
end


function env_value(grid_scale_value, f_draft, gs)
    return grid_scale_value - draft_sum(f_draft, gs)
end



"""
    ᶜspecific_env_value(χ_name, Y, p)

Calculates the specific value of a quantity `χ` in the environment (`χ⁰`).

This function uses the domain decomposition principle to first find the
density-area-weighted environment value (`ρa⁰χ⁰`) and the environment
density-weighted environmental area (`ρa⁰`). It then computes the specific value using the
regularized `specific` function, which provides a stable result even when the
environment area fraction is very small.

Arguments:

  - `χ_name`: A `MatrixFields.FieldName`, containing name for the specific quantity `χ` (e.g., `@name(h_tot)`, `@name(q_tot)`).
  - `Y`: The state, containing grid-mean and draft subdomain states.
  - `p`: The cache, containing precomputed quantities and turbconv_model.

Returns:

  - The specific value of the quantity `χ` in the environment.
"""
function ᶜspecific_env_value(χ_name, Y, p)
    turbconv_model = p.atmos.turbconv_model

    # Grid-scale density-weighted variable name, e.g., ρq_tot
    ρχ_name = get_ρχ_name(χ_name)

    ᶜρχ = MatrixFields.get_field(Y.c, ρχ_name)

    # environment density-area-weighted mse (`ρa⁰χ⁰`).
    # Numerator: ρa⁰χ⁰ = ρχ - (Σ ρaʲ * χʲ)
    if turbconv_model isa PrognosticEDMFX
        #Numerator: ρa⁰χ⁰ = ρχ - (Σ sgsʲ.ρa * sgsʲ.χ)

        ᶜρaχ⁰ = ᶜenv_value(
            ᶜρχ,
            sgsʲ ->
                MatrixFields.get_field(sgsʲ, @name(ρa)) *
                MatrixFields.get_field(sgsʲ, χ_name),
            Y.c.sgsʲs,
        )
        # Denominator: ρa⁰ = ρ - Σ ρaʲ
        ᶜρa⁰ = @. lazy(ρa⁰(Y.c.ρ, Y.c.sgsʲs, turbconv_model))

    elseif turbconv_model isa EDOnlyEDMFX
        error("Not implemented. You should use grid mean values.")
    end

    return @. lazy(specific(
        ᶜρaχ⁰,                      # ρaχ for environment
        ᶜρa⁰,                   # ρa for environment
        ᶜρχ,               # Fallback ρχ is the grid-mean value
        Y.c.ρ,                      # Fallback ρ is the grid-mean value
        turbconv_model,
    ))
end

"""
    get_ρχ_name(χ_name::FieldName)

Construct the `FieldName` corresponding to the product ρ·χ.

Given a tracer name `χ_name`, this function returns the new name that
represents the corresponding density-weighted quantity (ρ times χ).

The function works recursively on hierarchical field names: If `χ_name`
is a base name (no children), it returns a new name prefixed with `ρ`.
If `χ_name` has internal structure (e.g. a composite name), the function
recurses into the child names and prepends `ρ` at the lowest level.
"""
function get_ρχ_name(χ_name)
    parent_name = MatrixFields.FieldName(MatrixFields.extract_first(χ_name))
    child_name = MatrixFields.drop_first(χ_name)
    ρχ_name =
        (child_name == MatrixFields.@name()) ?
        MatrixFields.FieldName(Symbol(:ρ, MatrixFields.extract_first(χ_name))) :
        MatrixFields.append_internal_name(parent_name, get_ρχ_name(child_name))

    return ρχ_name
end

"""
    get_χʲ_name_from_ρχ_name(ρχ_name::FieldName)

Construct the `FieldName` corresponding to the specific tracer in the updraft
(`χʲ` with j = 1) associated with a given density-weighted tracer on the grid mean (`ρ·χ`).

Given the name of a density-weighted tracer `ρχ_name`, this function returns the
corresponding name of the specific tracer in the first subgrid updraft.

The function operates recursively on hierarchical field names:

  - If `ρχ_name` is a base name (no children), it replaces the `ρ` prefix with the
    appropriate updraft-specific prefix (e.g. `sgsʲs.:(1)`) and converts the variable
    to its specific form.
  - If `ρχ_name` has internal structure (e.g. a composite name), the function
    recurses into the child names and applies the transformation at the lowest level.
"""
function get_χʲ_name_from_ρχ_name(ρχ_name)
    parent_name = MatrixFields.FieldName(MatrixFields.extract_first(ρχ_name))
    child_name = MatrixFields.drop_first(ρχ_name)
    χʲ_name =
        (child_name == MatrixFields.@name()) ?
        MatrixFields.append_internal_name(
            @name(sgsʲs.:(1)),
            specific_tracer_name(ρχ_name),
        ) :
        MatrixFields.append_internal_name(parent_name, get_χʲ_name_from_ρχ_name(child_name))
    return χʲ_name
end

"""
    ρa⁰(ρ, sgsʲs, turbconv_model)

Computes the environment area-weighted density (`ρa⁰`).

This function calculates the environment area-weighted density by subtracting the sum of all draft subdomain area-weighted densities (`ρaʲ`) from the grid-mean density (`ρ`), following the domain decomposition principle (`GridMean = Environment + Sum(Drafts)`).

Arguments:
- `ρ`: Grid-mean density.
- `sgsʲs`: Iterable of draft subdomain quantities.
    - For `PrognosticEDMFX`: typically `Y.c.sgsʲs`
- `turbconv_model`: The turbulence convection model (e.g., `PrognosticEDMFX`, or others).

Returns:
- The area-weighted density of the environment (`ρa⁰`).
"""

function ρa⁰(ρ, sgsʲs, turbconv_model)
    # ρ - Σ ρaʲ
    if turbconv_model isa PrognosticEDMFX
        return env_value(ρ, sgsʲ -> sgsʲ.ρa, sgsʲs)
    else
        return ρ
    end
end

"""
    a⁰(sgsʲs, ᶜρʲs, turbconv_model)

Computes the environment area fraction (`a⁰`).

This function calculates the environment area fraction by subtracting the sum of all draft subdomain area fractions (`aʲ`) from 1 for `PrognosticEDMFX`, or returns 1 otherwise.

Arguments:

  - `sgsʲs`: Iterable of draft subdomain quantities.

      + For `PrognosticEDMFX`: typically `Y.c.sgsʲs`

  - `ᶜρʲs`: Iterable of draft densities.

      + Typically `p.precomputed.ᶜρʲs`
  - `turbconv_model`: The turbulence convection model (e.g., `PrognosticEDMFX`, or others).

Returns:

  - The area fraction of the environment (`a⁰`).
"""
function a⁰(sgsʲs, ᶜρʲs, turbconv_model)
    if turbconv_model isa PrognosticEDMFX
        return 1 - mapreduce_with_init(
            (sgsʲ, ᶜρʲ) -> draft_area(sgsʲ.ρa, ᶜρʲ),
            +,
            sgsʲs,
            ᶜρʲs,
        )
    else
        return 1
    end
end

"""
    ᶜspecific_env_mse(Y, p)

Computes the specific moist static energy (`mse`) in the environment (`mse⁰`).

This is a specialized helper function because `mse` is not a grid-scale prognostic
variable. It first computes the grid-scale moist static energy density (`ρmse`)
from other grid-scale quantities (`ρ`, total specific enthalpy `h_tot`, specific
kinetic energy `K`). It then uses the `ᶜenv_value` helper to compute the environment's
portion of `ρmse` and `ρa` via domain decomposition, and finally calculates the specific
value using the regularized `specific` function.

Arguments:

  - `Y`: The state containing `Y.c.ρ` and `Y.c.sgsʲs` (for PrognosticEDMFX).
  - `p`: The cache, containing the turbconv_model and precomputed quantities.

Returns:

  - A `ClimaCore.Fields.Field` containing the specific moist static energy of the
    environment (`mse⁰`).
"""
function ᶜspecific_env_mse(Y, p)
    turbconv_model = p.atmos.turbconv_model
    (; ᶜK, ᶜh_tot) = p.precomputed

    # grid-scale moist static energy density `ρ * mse`.
    ᶜρmse = @. lazy(Y.c.ρ * (ᶜh_tot - ᶜK))

    # environment density-area-weighted mse (`ρa⁰mse⁰`).
    # Numerator: ρa⁰mse⁰ = ρmse - (Σ ρaʲ * mseʲ)

    if turbconv_model isa PrognosticEDMFX
        ρa⁰mse⁰ = ᶜenv_value(ᶜρmse, sgsʲ -> sgsʲ.ρa * sgsʲ.mse, Y.c.sgsʲs)
        ᶜρa⁰ = @. lazy(ρa⁰(Y.c.ρ, Y.c.sgsʲs, turbconv_model))
    elseif turbconv_model isa EDOnlyEDMFX
        error("Not implemented. You should use grid mean values.")
    end

    return @. lazy(specific(ρa⁰mse⁰, ᶜρa⁰, ᶜρmse, Y.c.ρ, turbconv_model))
end

"""
    u₃⁰(ρaʲs, u₃ʲs, ρ, u₃, turbconv_model)

Computes the environment vertical velocity `u₃⁰`.

This function calculates the environment's total vertical momentum (`ρa⁰u₃⁰`) and
its total area-weighted density (`ρa⁰`) using the domain decomposition principle
(GridMean = Env + Sum(Drafts)). It then computes the final specific velocity `u₃⁰`
using the regularized `specific` function to ensure numerical stability when the
environment area fraction `a⁰` is small.

Arguments:

  - `ρaʲs`: A tuple of area-weighted densities for each draft subdomain.
  - `u₃ʲs`: A tuple of vertical velocities for each draft subdomain.
  - `ρ`: The grid-mean air density.
  - `u₃`: The grid-mean vertical velocity.
  - `turbconv_model`: The turbulence convection model, containing regularization parameters.
"""
u₃⁰(ρaʲs, u₃ʲs, ρ, u₃, turbconv_model) = specific(
    ρ * u₃ - unrolled_dotproduct(ρaʲs, u₃ʲs),
    ρ - reduce(+, ρaʲs),
    ρ * u₃,
    ρ,
    turbconv_model,
)

"""
    mapreduce_with_init(f, op, iter...)

A wrapper for Julia's `mapreduce` function that automatically determines
the initial value (`init`) for the reduction.

This is useful for iterators whose elements are custom structs or
`ClimaCore.Geometry.AxisTensor`s, where the zero element cannot be inferred
as a simple scalar. It uses `ClimaCore.RecursiveApply` tools (`rzero`,
`rpromote_type`) to create a type-stable, correctly-structured zero element
based on the output of the function `f` applied to the first elements of the
iterators.

Arguments:

  - `f`: The function to apply to each element.
  - `op`: The reduction operator (e.g., `+`, `*`).
  - `iter...`: One or more iterators.
"""
function mapreduce_with_init(f, op, iter...)
    r₀ = rzero(rpromote_type(typeof(f(map(first, iter)...))))
    mapreduce(f, op, iter...; init = r₀)
end

"""
    unrolled_dotproduct(a::Tuple, b::Tuple)

Computes the dot product of two `Tuple`s (`a` and `b`) using a recursive,
manually unrolled implementation.

This function is designed to be type-stable and efficient for CUDA kernels,
where standard `mapreduce` implementations can otherwise suffer from type-inference
failures.

It uses `ClimaCore.RecursiveApply` operators (`⊞` for addition, `⊠` for
multiplication), which allows it to handle dot products of tuples containing
complex, nested types such as `ClimaCore.Geometry.AxisTensor`s.

Arguments:

  - `a`: The first `Tuple`.
  - `b`: The second `Tuple`, which must have the same length as `a`.

Returns:

  - The result of the dot product, `Σᵢ a[i] * b[i]`.
"""
promote_type_mul(n::Number, x::Geometry.AxisTensor) = typeof(x)
promote_type_mul(x::Geometry.AxisTensor, n::Number) = typeof(x)
@inline function unrolled_dotproduct(a::Tuple, b::Tuple)
    r = rzero(promote_type_mul(first(a), first(b)))
    unrolled_dotproduct(r, a, b)
end
@inline unrolled_dotproduct(s, ::Tuple{}, ::Tuple{}) = s
@inline unrolled_dotproduct(s, a::Tuple, b::Tuple) =
    s ⊞ (first(a) ⊠ first(b)) ⊞
    unrolled_dotproduct(s, Base.tail(a), Base.tail(b))
@inline unrolled_dotproduct(s, a::Tuple{<:Any}, b::Tuple{<:Any}) =
    s ⊞ (first(a) ⊠ first(b))
