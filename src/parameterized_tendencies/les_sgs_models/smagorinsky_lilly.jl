#####
##### Smagorinsky Lilly Diffusion
#####

import ClimaCore.Fields as Fields
import ClimaCore: Geometry

"""
    lilly_stratification_correction(Y, p, ᶜS)

Return a lazy representation of the Lilly stratification correction factor
based on the local Richardson number.

# Arguments

  - `Y`: The model state.
  - `p`: The model parameters, e.g. `AtmosCache`.
  - `ᶜS`: The cell-centered strain rate tensor.
"""
function lilly_stratification_correction(Y, p, ᶜS)
    (; ᶜT, ᶜq_tot_nonneg, ᶜq_liq, ᶜq_ice) = p.precomputed
    (; ᶜtemp_scalar) = p.scratch
    grav = CAP.grav(p.params)
    Pr_t = CAP.Prandtl_number_0(CAP.turbconv_params(p.params))
    thermo_params = CAP.thermodynamics_params(p.params)
    FT = eltype(Pr_t)
    # Stratification correction
    ᶜθ_v = @. lazy(
        TD.virtual_pottemp(
            thermo_params,
            ᶜT,
            Y.c.ρ,
            ᶜq_tot_nonneg,
            ᶜq_liq,
            ᶜq_ice,
        ),
    )
    ᶜ∇ᵥθ = @. ᶜtemp_scalar = Geometry.WVector(ᶜgradᵥ(ᶠinterp(ᶜθ_v))).components.data.:1
    ᶜN² = @. lazy(grav / ᶜθ_v * ᶜ∇ᵥθ)
    ᶜS_norm = strain_rate_norm(ᶜS, Geometry.WAxis())

    ᶜRi = @. lazy(ᶜN² / (ᶜS_norm^2 + eps(FT)))  # Ri = N² / |S|²
    ᶜfb = @. lazy(ifelse(ᶜRi ≤ 0, FT(1), sqrt(sqrt(max(0, 1 - ᶜRi / Pr_t)))))
end

"""
    set_smagorinsky_lilly_precomputed_quantities!(Y, p)

Compute the Smagorinsky-Lilly horizontal and vertical quantities needed for
subgrid-scale diffusive tendencies

The subgrid-scale momentum flux tensor is defined by `τ = -2 νₜ ∘ S`,
where `νₜ` is the Smagorinsky-Lilly eddy viscosity and `S` is the strain rate tensor.

The turbulent diffusivity is defined as `D = νₜ / Pr_t`,
where `Pr_t` is the turbulent Prandtl number for neutral stratification.

This method precomputes and stores in `p.precomputed` the following quantities:

  - strain on centers and faces: `ᶜS`, `ᶠS`

  - horizontal and vertical strain rate norm, eddy viscosities, and diffusivities, on centers:

      + `ᶜS_norm_h`, `ᶜS_norm_v`, `ᶜνₜ_h`, `ᶜνₜ_v`, `ᶜD_h`, `ᶜD_v`

# Arguments

  - `Y`: The model state.
  - `p`: The model parameters, e.g. `AtmosCache`.
  - `model`: The Smagorinsky model type
"""
function set_smagorinsky_lilly_precomputed_quantities!(Y, p, model)
    (; ᶜu, ᶠu, ᶜS, ᶠS, ᶜL_h, ᶜL_v, ᶜS_norm_h, ᶜS_norm_v, ᶜνₜ_h, ᶜνₜ_v, ᶜD_h, ᶜD_v) =
        p.precomputed
    (; ᶜtemp_scalar) = p.scratch
    c_smag = CAP.c_smag(p.params)

    # Precompute 3D strain rate tensor
    compute_strain_rate_center_full!(ᶜS, ᶜu, ᶠu)
    compute_strain_rate_face_full!(ᶠS, ᶜu, ᶠu)

    # filter scale
    h_space = Spaces.horizontal_space(axes(Y.c))
    Δx = Δy = Spaces.node_horizontal_length_scale(h_space)
    ᶜΔz = Fields.Δz_field(Y.c)
    ax_xy = is_smagorinsky_UVW_coupled(model) ? Geometry.UVWAxis() : Geometry.UVAxis()
    ax_z = is_smagorinsky_UVW_coupled(model) ? Geometry.UVWAxis() : Geometry.WAxis()

    ᶜfb = lilly_stratification_correction(Y, p, ᶜS)
    if is_smagorinsky_UVW_coupled(model)
        ᶜL_h = ᶜL_v = @. lazy(c_smag * cbrt(Δx * Δy * ᶜΔz) * ᶜfb)
    else
        ᶜL_h = @. lazy(c_smag * Δx)
        ᶜL_v = @. lazy(c_smag * ᶜΔz * ᶜfb)
    end

    # Cache strain rate norms for diagnostics
    ᶜS_norm_h .= strain_rate_norm(ᶜS, ax_xy)
    ᶜS_norm_v .= strain_rate_norm(ᶜS, ax_z)

    # Smagorinsky eddy viscosity
    @. ᶜνₜ_h = ᶜL_h^2 * ᶜS_norm_h
    @. ᶜνₜ_v = ᶜL_v^2 * ᶜS_norm_v

    # Turbulent diffusivity
    Pr_t = CAP.Prandtl_number_0(CAP.turbconv_params(p.params))
    @. ᶜD_h = ᶜνₜ_h / Pr_t
    @. ᶜD_v = ᶜνₜ_v / Pr_t

    nothing
end
set_smagorinsky_lilly_precomputed_quantities!(Y, p, ::Nothing) = nothing

horizontal_smagorinsky_lilly_tendency!(Yₜ, Y, p, t, ::Nothing) = nothing
vertical_smagorinsky_lilly_tendency!(Yₜ, Y, p, t, ::Nothing) = nothing

function horizontal_smagorinsky_lilly_tendency!(Yₜ, Y, p, t, model::SmagorinskyLilly)
    is_smagorinsky_horizontal(model) || return nothing
    (; ᶜS, ᶠS, ᶜνₜ_h, ᶜD_h) = p.precomputed
    (; ᶜtemp_UVWxUVW, ᶠtemp_UVWxUVW, ᶜtemp_scalar, ᶠtemp_scalar) = p.scratch
    thermo_params = CAP.thermodynamics_params(p.params)
    ᶜρ = Y.c.ρ
    ᶠρ = @. ᶠtemp_scalar = ᶠinterp(ᶜρ)

    # Subgrid-scale momentum flux tensor, `τ = -2 νₜ ∘ S`
    ᶠνₜ_h = @. lazy(ᶠinterp(ᶜνₜ_h))
    ᶜτ_smag = @. ᶜtemp_UVWxUVW = -2 * ᶜνₜ_h * ᶜS  # TODO: Lazify once we can mix lazy horizontal & vertical operations
    ᶠτ_smag = @. ᶠtemp_UVWxUVW = -2 * ᶠνₜ_h * ᶠS

    # Apply to tendencies
    ## Horizontal momentum tendency
    @. Yₜ.c.uₕ -= C12(wdivₕ(ᶜρ * ᶜτ_smag) / ᶜρ)
    ## Vertical momentum tendency
    @. Yₜ.f.u₃ -= C3(wdivₕ(ᶠρ * ᶠτ_smag) / ᶠρ)

    ## Total energy tendency
    # The flux uses the dry-static-energy + water-enthalpy decomposition;
    # see `edmfx_sgs_diffusive_flux_tendency!` for the rationale.
    (; ᶜΦ) = p.core
    (; ᶜT, ᶜq_tot_nonneg, ᶜq_liq, ᶜq_ice) = p.precomputed
    ᶜq_vap = @. lazy(TD.vapor_specific_humidity(ᶜq_tot_nonneg, ᶜq_liq, ᶜq_ice))
    ᶜ∇h_tot = ᶜtotal_enthalpy_gradientₕ!(
        p.scratch.ᶜtemp_C12, thermo_params, ᶜT, ᶜΦ, ᶜq_vap, ᶜq_liq, ᶜq_ice,
    )
    @. Yₜ.c.ρe_tot += wdivₕ(ᶜρ * ᶜD_h * ᶜ∇h_tot)

    ## Tracer diffusion and associated mass changes
    foreach_gs_tracer(Yₜ, Y) do ᶜρχₜ, ᶜρχ, ρχ_name
        ᶜχ = @. lazy(specific(ᶜρχ, ᶜρ))
        ᶜ∇ₕρD∇χₜ = @. lazy(wdivₕ(ᶜρ * ᶜD_h * gradₕ(ᶜχ)))
        @. ᶜρχₜ += ᶜ∇ₕρD∇χₜ
        # Rain and snow does not affect the mass
        if ρχ_name == @name(ρq_tot)
            @. Yₜ.c.ρ += ᶜ∇ₕρD∇χₜ
        end
    end
end

function vertical_smagorinsky_lilly_tendency!(Yₜ, Y, p, t, model::SmagorinskyLilly)
    is_smagorinsky_vertical(model) || return nothing
    (; ᶜS, ᶠS, ᶜνₜ_v) = p.precomputed
    (; ᶜtemp_UVWxUVW, ᶠtemp_UVWxUVW, ᶠtemp_scalar, ᶠtemp_scalar_2) = p.scratch
    Pr_t = CAP.Prandtl_number_0(CAP.turbconv_params(p.params))
    ᶜρ = Y.c.ρ
    ᶠρ = @. ᶠtemp_scalar = ᶠinterp(ᶜρ)

    # Subgrid-scale momentum flux tensor, `τ = -2 νₜ ∘ S`
    ᶠνₜ_v = @. lazy(ᶠinterp(ᶜνₜ_v))
    ᶜτ_smag = @. ᶜtemp_UVWxUVW = -2 * ᶜνₜ_v * ᶜS
    ᶠτ_smag = @. ᶠtemp_UVWxUVW = -2 * ᶠνₜ_v * ᶠS

    # Turbulent diffusivity
    ᶠD_smag = @. lazy(ᶠνₜ_v / Pr_t)
    ᶠρD = @. lazy(ᶠρ * ᶠD_smag)

    # Apply to tendencies
    ## Horizontal momentum tendency
    @. Yₜ.c.uₕ -= C12(ᶜdivᵥ(ᶠρ * ᶠτ_smag) / ᶜρ)
    ## Vertical momentum tendency
    @. Yₜ.f.u₃ -= C3(ᶠdiffdivᵥ_u₃(ᶜρ * ᶜτ_smag) / ᶠρ)

    ## Total energy tendency
    # The flux uses the dry-static-energy + water-enthalpy decomposition;
    # see `edmfx_sgs_diffusive_flux_tendency!` for the rationale.
    thermo_params = CAP.thermodynamics_params(p.params)
    (; ᶜΦ) = p.core
    (; ᶜT, ᶜq_tot_nonneg, ᶜq_liq, ᶜq_ice) = p.precomputed
    ᶜq_vap = @. lazy(TD.vapor_specific_humidity(ᶜq_tot_nonneg, ᶜq_liq, ᶜq_ice))
    ᶠ∇ᵥh_tot =
        ᶠtotal_enthalpy_gradientᵥ(thermo_params, ᶜT, ᶜΦ, ᶜq_vap, ᶜq_liq, ᶜq_ice)
    @. Yₜ.c.ρe_tot -= ᶜdiffdivᵥ(-(ᶠρD * ᶠ∇ᵥh_tot))

    ## Tracer diffusion and associated mass changes
    foreach_gs_tracer(Yₜ, Y) do ᶜρχₜ, ᶜρχ, ρχ_name
        ᶜχ = @. lazy(specific(ᶜρχ, ᶜρ))
        ᶜ∇ᵥρD∇χₜ = ᶜdiffusive_flux_divergenceᵥ(ᶠρD, ᶜχ)
        @. ᶜρχₜ -= ᶜ∇ᵥρD∇χₜ
        # Rain and snow does not affect the mass
        if ρχ_name == @name(ρq_tot)
            @. Yₜ.c.ρ -= ᶜ∇ᵥρD∇χₜ
        end
    end
end
